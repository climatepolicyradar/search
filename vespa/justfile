# Run vespa dev
up:
    docker compose up -d

# Deploy Vespa application
deploy:
    vespa deploy ./app --target http://localhost:19071/ --wait 300

# Extract the data to load into Vespa
extract:
    if [ ! -f "../.data_cache/extract_vespa_updates/updates.jsonl" ]; then \
        uv run python scripts/extract_vespa_updates.py; \
    fi

extract_from_s3:
    # this is a way of sharing the output of `extract_vespa_updates` step as it is a very long running process
    aws s3 cp s3://cpr-cache/search/.data_cache/extract_vespa_updates/updates.jsonl ../.data_cache/extract_vespa_updates/updates.jsonl

update_extract_from_s3:
    aws s3 cp ../.data_cache/extract_vespa_updates/updates.jsonl s3://cpr-cache/search/.data_cache/extract_vespa_updates/updates.jsonl

# Load data into Vespa
feed limit="":
    if [ -z "{{limit}}" ]; then \
        echo "Feed all files into local Vespa"; \
        cat ../.data_cache/extract_vespa_updates/updates.jsonl | vespa feed - --target http://localhost:8080 --progress 30; \
    else \
        echo "Feed {{limit}} files into local Vespa"; \
        head -n {{limit}} ../.data_cache/extract_vespa_updates/updates.jsonl | vespa feed - --target http://localhost:8080 --progress 30; \
    fi

# Dev
dev:
    just up
    just deploy
    just feed 100

get query="congo":
    #!/usr/bin/env bash
    set -e
    curl -G "http://localhost:8080/search/" \
     --data-urlencode 'yql=select * from sources documents where userQuery()' \
     --data-urlencode 'query={{query}}' \
     --data-urlencode 'hits=10'

# Remove the application. To remove data as well, use `reset` below.
destroy:
    curl -X DELETE http://localhost:19071/application/v2/tenant/default/application/default

# Remove the Vespa container and volume
reset:
    docker compose down -v

# remote command
remote-deploy:
    #!/usr/bin/env bash
    set -e
    # read this parameter from aws smm and store it in a variable it's a secret
    vespa_endpoint=$(aws ssm get-parameter --name "/search/vespa/endpoint" --query "Parameter.Value" --output text --with-decryption)
    vespa_application=$(aws ssm get-parameter --name "/search/vespa/application" --query "Parameter.Value" --output text --with-decryption)
    vespa prod deploy ./app --target cloud --application $vespa_application --zone prod.aws-eu-west-1a

remote-feed length="100":
    #!/usr/bin/env bash
    set -e
    vespa_write_token=$(aws ssm get-parameter --name "/search/vespa/write-token" --query "Parameter.Value" --output text --with-decryption)
    vespa_endpoint=$(aws ssm get-parameter --name "/search/vespa/endpoint" --query "Parameter.Value" --output text --with-decryption)
    head -{{length}} ../.data_cache/extract_vespa_updates/updates.jsonl | vespa feed - --target $vespa_endpoint --header "Authorization: Bearer $vespa_write_token"

remote-feed-all:
    #!/usr/bin/env bash
    set -e
    vespa_write_token=$(aws ssm get-parameter --name "/search/vespa/write-token" --query "Parameter.Value" --output text --with-decryption)
    vespa_endpoint=$(aws ssm get-parameter --name "/search/vespa/endpoint" --query "Parameter.Value" --output text --with-decryption)
    vespa feed ../.data_cache/extract_vespa_updates/updates.jsonl --target $vespa_endpoint --header "Authorization: Bearer $vespa_write_token"

remote-visit:
    #!/usr/bin/env bash
    set -e
    vespa_read_token=$(aws ssm get-parameter --name "/search/vespa/read-token" --query "Parameter.Value" --output text --with-decryption)
    vespa_endpoint=$(aws ssm get-parameter --name "/search/vespa/endpoint" --query "Parameter.Value" --output text --with-decryption)
    vespa visit $vespa_endpoint --header "Authorization: Bearer $vespa_read_token"
